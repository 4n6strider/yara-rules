/*
Generically detect exploitation of CVE-2018-4878, a use-after-free vulnerability affecting Adobe Flash versions up to
and including 28.0.0.137. Following the conversation at:

    http://blog.inquest.net/blog/2018/02/07/cve-2018-4878-adobe-flash-0day-itw
    https://twitter.com/i/moments/960633253165191170

 InQuest customers can detect related events on their network by searching for:
 
    event ID 5000805
*/

rule Adobe_Flash_DRM_Use_After_Free
{    
    meta:
        note  = "This YARA rule is intended to run atop of decompiled Flash."
    strings:
        $as   = "package"
        $exp1 = "import com.adobe.tvsdk.mediacore.MediaPlayer"
        $exp2 = "createDispatcher("
        $exp3 = "createMediaPlayer("
        $exp4 = "drmManager.initialize("     // com.adobe.tvsdk.mediacore.DRMOperationCompleteListener;
        $exp5 = "push(this)"
        $exp6 = "push(null)"
        $exp7 = /(pop\(\)\..+\s*=\s*.+pop\(\)|push\([^\)]{1,24}drmManager.initialize)/
    condition:
        $as at 0 and all of them
}

